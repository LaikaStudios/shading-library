/*
 *  Copyright(c) 2009 Laika, developed under contract by mitchpraterLLC.
 *  All rights reserved unless otherwise granted. This program or documentation
 *  contains proprietary, confidential information and trade secrets of Laika
 *  and/or mitchpraterLLC. Use of copyright notice does not imply publication.
 *  
 *  By possessing this software, you agree to the following terms and conditions.
 *  You may not resell this software, but you may freely distribute it to others.
 *  You may use it for whatever purposes you wish. There are no warranties of any
 *  kind. You may make modifications, however any derivation may only be distributed
 *  to others under the same freedoms granted to you herein, it must retain this
 *  notice and agreement, and the modifications noted. We also ask that you make
 *  such modifications available to Laika, mitchpraterLLC, or to the computer
 *  graphics community as a whole through a public forum.
 */
//
//  Make objects double shaded.
//
//  Works on only the selected objects,
//  or the whole scene if nothing is selected.
//
global proc makeDoubleShaded()
{
    string $currentSelection[] = `ls -selection`;

    string $convertedNodes[];
    string $geoNodes[];

    if (size($currentSelection) > 0) {
        select -hierarchy;
        $geoNodes = `ls -selection -geometry`;
    }
    else
        $geoNodes = `ls -geometry`;

    string $geoNode;
    for ($geoNode in $geoNodes) {
        setAttr ($geoNode + ".doubleSided", 0);
        if (!`attributeExists "rman__riattr__sides_doubleshaded" $geoNode`) {
            select -replace $geoNode;
            addAttr -longName "rman__riattr__sides_doubleshaded";
        }
        if (`getAttr ($geoNode + ".rman__riattr__sides_doubleshaded")` == 0) {
            setAttr ($geoNode + ".rman__riattr__sides_doubleshaded", 1);
            $convertedNodes[size($convertedNodes)] = $geoNode;
        }
    }

    if (size($convertedNodes) > 0) {
        print ("Made these objects Double Shaded:\n");
        print ($convertedNodes);
        print ("\n");

        select -replace $convertedNodes;
    }
    else {
        select -replace $currentSelection;

        if (size($currentSelection) == 0)
            print ("Found no objects to make Double Shaded.\n\n");
        else
            print ("Found no selected objects to make Double Shaded.\n\n");
    }
}
