/*
 *  Copyright(c) 2009 Laika, developed under contract by mitchpraterLLC.
 *  All rights reserved unless otherwise granted. This program or documentation
 *  contains proprietary, confidential information and trade secrets of Laika
 *  and/or mitchpraterLLC. Use of copyright notice does not imply publication.
 *  
 *  By possessing this software, you agree to the following terms and conditions.
 *  You may not resell this software, but you may freely distribute it to others.
 *  You may use it for whatever purposes you wish. There are no warranties of any
 *  kind. You may make modifications, however any derivation may only be distributed
 *  to others under the same freedoms granted to you herein, it must retain this
 *  notice and agreement, and the modifications noted. We also ask that you make
 *  such modifications available to Laika, mitchpraterLLC, or to the computer
 *  graphics community as a whole through a public forum.
 */
//
//  Select and print a list of objects with their opposite flag set.
//
//  Works on only the selected objects,
//  or the whole scene if nothing is selected.
//
global proc reportOpposite()
{
    string $currentSelection[] = `ls -selection`;

    string $oppositeNodes[];
    string $geoNodes[];

    if (size($currentSelection) > 0) {
        select -hierarchy;
        $geoNodes = `ls -selection -geometry`;
    }
    else
        $geoNodes = `ls -geometry`;

    string $geoNode;
    for ($geoNode in $geoNodes) {
        if (`getAttr ($geoNode + ".opposite")`)
        $oppositeNodes[size($oppositeNodes)] = $geoNode;
    }

    if (size($oppositeNodes) > 0) {
        print ("These objects should have their normals re-oriented rather than relying on the Opposite attribute:\n");
        print ($oppositeNodes);
        print ("\n");

        select -replace $oppositeNodes;
    }
    else {
        select -replace $currentSelection;

        if (size($currentSelection) == 0)
            print ("Found no objects with the Opposite attribute set.\n\n");
        else
            print ("No selected objects have their Opposite attribute set.\n\n");
    }
}
